//@version=6
indicator("Average Directional Index", shorttitle="ADX", format=format.price, precision=2)

const color BULLISH_RISING_COLOR = color.new(#14b8a6, 0)  // teal-500
const color BULLISH_FALLING_COLOR = color.new(#5eead4, 0) // teal-300

const color BEARISH_RISING_COLOR = color.new(#fda4af, 0)  // rose-300
const color BEARISH_FALLING_COLOR = color.new(#f43f5e, 0) // rose-500

// Settings
diLength = input.int(14, title="DI Length", minval=2, step=1)
adxSmoothing = input.int(14, title="ADX Smoothing", minval=2, step=1)

var g1 = "Histogram"
maKind = input.string("RMA", title="Moving Average Kind", options=["None", "SMA", "EMA", "RMA", "HMA", "WMA", "VWMA"], group=g1)
maLength = input.int(3, title="Moving Average Length", minval=2, step=1, group=g1)


// Calculate moving average by kind if not None
maByKind(src, len, k) =>
    switch k
        "SMA"  => ta.sma(src, len)
        "EMA"  => ta.ema(src, len)
        "RMA"  => ta.rma(src, len)
        "HMA"  => ta.hma(src, len)
        "WMA"  => ta.wma(src, len)
        "VWMA" => ta.vwma(src, len)
        => src


[plus, minus, adx] = ta.dmi(diLength, adxSmoothing)

// Apply moving average to the DI difference for histogram
histogram = maByKind(plus - minus, maLength, maKind)

// Alerts
alertcondition(
  histogram[1] >= 0 and histogram < 0,
  title="Rising to falling",
  message="The ADX switched from a rising to falling state")
alertcondition(
  histogram[1] <= 0 and histogram > 0,
  title="Falling to rising",
  message="The ADX switched from a falling to rising state")

// Plot ADX line
plot(adx, title="Line", color=color.new(#3b82f6, 0)) // blue-500

// Plot ADX histogram
plot(
  histogram,
  title="Histogram",
  style=plot.style_columns,
  color=histogram >= 0 ?
      (histogram[1] < histogram ? BULLISH_RISING_COLOR : BULLISH_FALLING_COLOR) :
      (histogram[1] < histogram ? BEARISH_RISING_COLOR : BEARISH_FALLING_COLOR))

// Add zero line
hline(0, title="Zero", color=color.new(#6b7280, 50)) // gray-500/50
