//@version=6
indicator(title="Supertrend", overlay=true, timeframe="", timeframe_gaps=true)

const color BULLISH_LINE_COLOR = color.new(#a3e635, 0) // lime-400
const color BEARISH_LINE_COLOR = color.new(#f472b6, 0) // pink-400

const color BULLISH_FILL_COLOR = color.new(#bef264, 80) // lime-300
const color BEARISH_FILL_COLOR = color.new(#f9a8d4, 80) // pink-300

atrLength = input.int(10, title="ATR Length", minval=1, step=1)
factor = input.float(3.0, title="Factor", minval=0.01, step=0.01)
plotLines = input.bool(false, title="Plot Supertrend lines")
fillArea = input.bool(true, title="Fill trend area", tooltip="Fill the area between the Supertrend line and the candlestick body (default: true)")

[supertrend, direction] = ta.supertrend(factor, atrLength)
supertrend := barstate.isfirst ? na : supertrend

isUpTrend = direction < 0
upTrend = plot(
  isUpTrend ? supertrend : na,
  title="Up Trend",
  color=BULLISH_LINE_COLOR,
  style=plot.style_linebr,
  display=plotLines ? display.all : display.none)

isDownTrend = direction > 0
downTrend = plot(
  isDownTrend ? supertrend : na,
  title="Down Trend",
  color=BEARISH_LINE_COLOR,
  style=plot.style_linebr,
  display=plotLines ? display.all : display.none)

// Ensure the fill doesn't overlap with the candles
middleLineSeries = isUpTrend
  ? (close < open ? close : open) // from supertrend to bottom of candle body
  : (close > open ? close : open) // from supertrend to top of candle body
middleLine = plot(
  barstate.isfirst ? na : middleLineSeries,
  title="Middle Line",
  display=display.none)

fill(
  plot1=middleLine,
  plot2=upTrend,
  title="Up Fill",
  color=BULLISH_FILL_COLOR,
  fillgaps=false,
  display=fillArea ? display.all : display.none)

fill(
  plot1=middleLine,
  plot2=downTrend,
  title="Down Fill",
  color=BEARISH_FILL_COLOR,
  fillgaps=false,
  display=fillArea ? display.all : display.none)

alertcondition(
  direction[1] > direction,
  title="Downtrend to Uptrend",
  message="The Supertrend value switched from Downtrend to Uptrend")

alertcondition(
  direction[1] < direction,
  title="Uptrend to Downtrend",
  message="The Supertrend value switched from Uptrend to Downtrend")

alertcondition(
  direction[1] != direction,
  title="Trend Change",
  message="The Supertrend value switched from Uptrend to Downtrend or vice versa")
