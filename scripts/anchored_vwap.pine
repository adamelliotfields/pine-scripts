//@version=6
indicator(title="Anchored VWAP", shorttitle="AVWAP", overlay=true)

source = input.source(hlc3, title="Source")
start = input.time(0, title="Start", confirm=true) // confirm enables interactive mode


calculateVWAP(anchor) =>
    // Initialize to na because of recursive reference
    float sourceVolume = na
    float totalVolume = na

    // Start if not already an anchor otherwise accumulate
    sourceVolume := anchor and not anchor[1] ? (source * volume) : sourceVolume[1] + (source * volume)
    totalVolume := anchor and not anchor[1] ? volume : (totalVolume[1] + volume)

    // VWAP is Σ(P * V) / Σ(V)
    sourceVolume / totalVolume


plot(calculateVWAP(start <= time), title="AVWAP", color=color.purple, linewidth=2)
